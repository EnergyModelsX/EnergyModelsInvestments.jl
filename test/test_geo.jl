# Definition of the individual resources used in the simple system
CO2     = ResourceEmit("CO2", 1.)
Power   = ResourceCarrier("Power", 0.)
products = [Power, CO2]

"""
Creates a simple geography test case with the potential for investments in transmission infrastructure
if provided with transmission investments through the argument `inv_data`.
"""
function small_graph_geo(; source=nothing, sink=nothing, inv_data=nothing)

    # Creation of a dictionary with entries of 0. for all resources
    𝒫₀ = Dict(k  => 0 for k ∈ products)

    # Creation of the source and sink module as well as the arrays used for nodes and links
    if isnothing(source)
        source = RefSource(
                    "-src",
                    FixedProfile(50),
                    FixedProfile(10),
                    FixedProfile(5),
                    Dict(Power => 1),
                    []
                )
    end

    if isnothing(sink)
        sink = RefSink(
                    "-snk",
                    StrategicFixedProfile([20, 25, 30, 35]),
                    Dict(:Surplus => FixedProfile(0), :Deficit => FixedProfile(1e6)),
                    Dict(Power => 1),
                )
    end

    nodes = [GeoAvailability(1, 𝒫₀, 𝒫₀), GeoAvailability(1, 𝒫₀, 𝒫₀), source, sink]
    links = [Direct(31, nodes[3], nodes[1], Linear())
             Direct(24, nodes[2], nodes[4], Linear())]
    
    # Creation of the two areas and potential transmission lines
    areas = [RefArea(1, "Oslo", 10.751, 59.921, nodes[1]), 
             RefArea(2, "Trondheim", 10.398, 63.4366, nodes[2])]        

    # Check if investments are included
    if isnothing(inv_data)
        inv_data = []
    else
        inv_data = [inv_data]
    end

    transmission_line = RefStatic("transline", Power, FixedProfile(10), FixedProfile(0.1), FixedProfile(0.0), FixedProfile(0.0), 1, inv_data)

    transmissions = [
                    Transmission(areas[1], areas[2], [transmission_line]),
                    ]

    # Creation of the time structure and the used global data
    T = UniformTwoLevel(1, 4, 1, UniformTimes(1, 1, 1))
    modeltype = InvestmentModel(
                            Dict(CO2 => StrategicFixedProfile([450, 400, 350, 300])),
                            Dict(CO2 => StrategicFixedProfile([0, 0, 0, 0])),
                            CO2,
                            0.07
                        )

    # Creation of the case dictionary
    case = Dict(
                :nodes          => nodes,
                :links          => links,
                :products       => products,
                :areas          => areas,
                :transmission   => transmissions,
                :T              => T,
                )

    return case, modeltype
end


"""
    optimize(cases)

Optimize the `case`.
"""
function optimize(case, modeltype)
    m = GEO.create_model(case, modeltype)
    set_optimizer(m, OPTIMIZER)
    optimize!(m)
    return m
end

# Test set for analysing the proper behaviour when no investment was included
@testset "Unidirectional transmission without investments" begin

    # Creation and run of the optimization problem
    case, modeltype = small_graph_geo()
    m               = optimize(case, modeltype)

    general_tests(m)

    # Extraction of required data
    𝒯    = case[:T]
    𝒯ᴵⁿᵛ = strategic_periods(𝒯)
    sink = case[:nodes][4]
    tr_osl_trd  = case[:transmission][1]
    trans_mode  = tr_osl_trd.Modes[1]

    # Test identifying that the proper deficit is calculated
    @test sum(value.(m[:sink_deficit][sink, t])
                        ≈ sink.Cap[t] - trans_mode.Trans_cap[t] for t ∈ 𝒯) == length(𝒯)
                        
    # Test showing that no investment variables are created
    @test size(m[:trans_cap_invest_b])[1] == 0
    @test size(m[:trans_cap_remove_b])[1] == 0
    @test size(m[:trans_cap_current])[1] == 0
    @test size(m[:trans_cap_add])[1] == 0
    @test size(m[:trans_cap_rem])[1] == 0

end

# Test set for continuous investments
@testset "Unidirectional transmission with ContinuousInvestment" begin

    # Creation and run of the optimization problem
    inv_data = IM.TransInvData(
                Capex_trans     = FixedProfile(10),     # capex [€/kW]
                Trans_max_inst  = FixedProfile(250),    # max installed capacity [kW]
                Trans_max_add   = FixedProfile(30),     # max_add [kW]
                Trans_min_add   = FixedProfile(0),      # min_add [kW]
                Inv_mode        = IM.ContinuousInvestment(),
                Trans_increment = FixedProfile(10),
                Trans_start     = 0,
            )

    case, modeltype = small_graph_geo(inv_data=inv_data)
    m               = optimize(case, modeltype)

    general_tests(m)

    # Extraction of required data
    𝒯    = case[:T]
    𝒯ᴵⁿᵛ = strategic_periods(𝒯)
    sink = case[:nodes][4]
    tr_osl_trd  = case[:transmission][1]
    trans_mode  = tr_osl_trd.Modes[1]

    # Test identifying that the there is no deficit
    @test sum(value.(m[:sink_deficit][sink, t])  == 0 for t ∈ 𝒯) == length(𝒯)
                        
    # Test showing that the investments are as expected
    for t_inv ∈ 𝒯ᴵⁿᵛ
        if TS.isfirst(t_inv)
            @testset "First investment period" begin
                for t ∈ t_inv
                    @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                                    ≈ sink.Cap[t]-inv_data.Trans_start)
                end
            end
        else
            @testset "Subsequent investment periods" begin
                for t ∈ t_inv
                    @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                            ≈ sink.Cap[t]-value.(m[:trans_cap_current][trans_mode, previous(t_inv, 𝒯)]))
                end
            end
        end
    end

end

# Test set for semicontinuous investments
@testset "Unidirectional transmission with SemiContinuousInvestment" begin

    # Creation and run of the optimization problem
    inv_data = IM.TransInvData(
                Capex_trans     = FixedProfile(10),     # capex [€/kW]
                Trans_max_inst  = FixedProfile(250),    # max installed capacity [kW]
                Trans_max_add   = FixedProfile(30),     # max_add [kW]
                Trans_min_add   = FixedProfile(10),     # min_add [kW]
                Inv_mode        = IM.SemiContinuousInvestment(),
                Trans_increment = FixedProfile(10),
                Trans_start     = 0,
            )

    case, modeltype = small_graph_geo(inv_data=inv_data)
    m                = optimize(case, modeltype)

    general_tests(m)

    # Extraction of required data
    𝒯    = case[:T]
    𝒯ᴵⁿᵛ = strategic_periods(𝒯)
    sink = case[:nodes][4]
    tr_osl_trd  = case[:transmission][1]
    trans_mode  = tr_osl_trd.Modes[1]

    # Test identifying that the there is no deficit
    @test sum(value.(m[:sink_deficit][sink, t])  == 0 for t ∈ 𝒯) == length(𝒯)
                        
    # Test showing that the investments are as expected
    for t_inv ∈ 𝒯ᴵⁿᵛ
        @testset "Investment period $(t_inv.sp)" begin
            @testset "Invested capacity" begin
                if TS.isfirst(t_inv)
                    for t ∈ t_inv
                        @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                                        >= max(sink.Cap[t] - inv_data.Trans_start, 
                                            inv_data.Trans_min_add[t] * value.(m[:trans_cap_invest_b][trans_mode, t_inv])))
                    end
                else
                    for t ∈ t_inv
                        @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                                        ⪆ max(sink.Cap[t] - value.(m[:trans_cap_current][trans_mode, previous(t_inv, 𝒯)]), 
                                    inv_data.Trans_min_add[t] * value.(m[:trans_cap_invest_b][trans_mode, t_inv])))
                    end
                end
            end

            # Test that the binary value is regulating the investments
            @testset "Binary value" begin
                if value.(m[:trans_cap_invest_b][trans_mode, t_inv]) == 0
                    @test value.(m[:trans_cap_add][trans_mode, t_inv]) == 0
                else
                    @test value.(m[:trans_cap_add][trans_mode, t_inv]) ⪆ 0
                end
            end
        end
    end

end

# Test set for semicontinuous investments with offsets in the cost
@testset "Unidirectional transmission with SemiContinuousOffsetInvestment" begin

    # Creation and run of the optimization problem
    inv_data = IM.TransInvData(
                Capex_trans     = FixedProfile(1),     # capex [€/kW]
                Capex_trans_offset = FixedProfile(10),    # capex [€]
                Trans_max_inst  = FixedProfile(250),    # max installed capacity [kW]
                Trans_max_add   = FixedProfile(30),     # max_add [kW]
                Trans_min_add   = FixedProfile(10),     # min_add [kW]
                Inv_mode        = IM.SemiContinuousOffsetInvestment(),
                Trans_increment = FixedProfile(10),
                Trans_start     = 0,
            )

    case, modeltype = small_graph_geo(inv_data=inv_data)
    m                = optimize(case, modeltype)

    general_tests(m)

    # Extraction of required data
    𝒯    = case[:T]
    𝒯ᴵⁿᵛ = strategic_periods(𝒯)
    sink = case[:nodes][4]
    tr_osl_trd  = case[:transmission][1]
    trans_mode  = tr_osl_trd.Modes[1]

    # Test identifying that the there is no deficit
    @test sum(value.(m[:sink_deficit][sink, t])  == 0 for t ∈ 𝒯) == length(𝒯)
                        
    # Test showing that the investments are as expected
    for t_inv ∈ 𝒯ᴵⁿᵛ
        @testset "Investment period $(t_inv.sp)" begin
            @testset "Invested capacity" begin
                if TS.isfirst(t_inv)
                    for t ∈ t_inv
                        @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                                        >= max(sink.Cap[t] - inv_data.Trans_start, 
                                            inv_data.Trans_min_add[t] * value.(m[:trans_cap_invest_b][trans_mode, t_inv])))
                    end
                else
                    for t ∈ t_inv
                        @test (value.(m[:trans_cap_add][trans_mode, t_inv]) 
                                        ⪆ max(sink.Cap[t] - value.(m[:trans_cap_current][trans_mode, previous(t_inv, 𝒯)]), 
                                    inv_data.Trans_min_add[t] * value.(m[:trans_cap_invest_b][trans_mode, t_inv])))
                    end
                end
            end

            # Test that the binary value is regulating the investments
            @testset "Binary value" begin
                if value.(m[:trans_cap_invest_b][trans_mode, t_inv]) == 0
                    @test value.(m[:trans_cap_add][trans_mode, t_inv]) == 0
                else
                    @test value.(m[:trans_cap_add][trans_mode, t_inv]) ⪆ 0
                end
            end
        end
    end
    @testset "Investment costs" begin
        @test sum(value(m[:trans_cap_add][trans_mode, t_inv]) * inv_data.Capex_trans[t_inv] + 
            inv_data.Capex_trans_offset[t_inv] * value(m[:trans_cap_invest_b][trans_mode, t_inv]) ≈ 
            value(m[:capex_trans][trans_mode, t_inv]) for t_inv ∈ 𝒯ᴵⁿᵛ, atol=TEST_ATOL) == length(𝒯ᴵⁿᵛ)
    end
end

# Test set for discrete investments
@testset "Unidirectional transmission with DiscreteInvestment" begin

    # Creation and run of the optimization problem
    inv_data = IM.TransInvData(
                Capex_trans     = FixedProfile(10),     # capex [€/kW]
                Trans_max_inst  = FixedProfile(250),    # max installed capacity [kW]
                Trans_max_add   = FixedProfile(30),     # max_add [kW]
                Trans_min_add   = FixedProfile(10),     # min_add [kW]
                Inv_mode        = IM.DiscreteInvestment(),
                Trans_increment = FixedProfile(5),
                Trans_start     = 5,
            )

    case, modeltype = small_graph_geo(inv_data=inv_data)
    m                = optimize(case, modeltype)

    general_tests(m)

    # Extraction of required data
    𝒯    = case[:T]
    𝒯ᴵⁿᵛ = strategic_periods(𝒯)
    sink = case[:nodes][4]
    tr_osl_trd  = case[:transmission][1]
    trans_mode  = tr_osl_trd.Modes[1]

    # Test identifying that the there is no deficit
    @test sum(value.(m[:sink_deficit][sink, t])  == 0 for t ∈ 𝒯) == length(𝒯)
                        
    # Test showing that the investments are as expected
    for t_inv ∈ 𝒯ᴵⁿᵛ
        @testset "Invested capacity $(t_inv.sp)" begin
            if value.(m[:trans_cap_invest_b][trans_mode, t_inv]) == 0
                @test value.(m[:trans_cap_add][trans_mode, t_inv]) == 0
            else
                @test value.(m[:trans_cap_add][trans_mode, t_inv]) ≈ 
                    inv_data.Trans_increment[t_inv] * value.(m[:trans_cap_invest_b][trans_mode, t_inv]) 
            end
        end
    end
end